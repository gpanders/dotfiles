" Greg Anders (gpanders)'s vimrc <https://github.com/gpanders/dotfiles.git>

" Clear all vimrc autocmds at the beginning
augroup vimrc | execute 'autocmd!' | augroup END

" Setup {{{

" Set $VIMHOME
if has('unix')
  let $VIMHOME = $HOME . '/.vim'
else
  let $VIMHOME = $HOME . '/vimfiles'
endif

" Use vim-pathogen if native packages are not supported
if !has('packages')
  " Use vim-pathogen to emulate native packaging behavior
  if empty(glob($VIMHOME . '/autoload/pathogen.vim'))
    silent execute '!curl -LSso ' . $VIMHOME . '/autoload/pathogen.vim https://tpo.pe/pathogen.vim'
  endif
  silent! execute pathogen#infect('pack/{}/start/{}', 'pack/{}/opt/{}')
endif

if has('autocmd')
  filetype plugin indent on
endif

if has('syntax') && !exists('g:syntax_on')
  syntax enable
endif
" }}}

" Colors {{{

" Try to make comments italic
autocmd vimrc ColorScheme * highlight Comment gui=italic cterm=italic

" Load colorscheme
silent! colorscheme base16-eighties

" Highlight trailing whitespace
highlight link TrailingWhitespace Error

" }}}

" Settings {{{

" Defaults {{{
if !has('nvim')
  " The following settings are the defaults in nvim. Set them also in vim for
  " a consistent experience between the two
  set autoindent
  set autoread
  set backspace=indent,eol,start  " Allow vim to delete whitespace
  silent! set belloff=all
  set complete-=i
  set display=lastline
  set nofsync
  set formatoptions+=j  " Remove comment character when joining lines
  set history=10000
  set hlsearch
  set incsearch
  set langnoremap
  silent! set nolangremap
  set laststatus=2
  set nrformats-=octal  " Don't interpret numbers starting with 0 as octal
  set ruler
  set sessionoptions-=options  " Don't save options in session files
  silent! set shortmess+=F
  set showcmd
  set sidescroll=1
  set smarttab
  set tabpagemax=50  " Increase maximum number of tab pages allowed
  set tags=./tags;,tags
  set ttimeout
  set ttimeoutlen=50
  set ttyfast
  set viminfo^=!  " Keep and store UPPERCASE global variables
  set wildmenu

  " Use UTF-8 if we can and env LANG didn't tell us not to
  if has('multi_byte') && !exists('$LANG') && &encoding ==# 'latin1'
    set encoding=utf-8
  endif

  " Set cursor shape based on mode (:h termcap-cursor-shape)
  " Vertical bar in insert mode
  let &t_SI = "\e[6 q"
  " Underline in replace mode
  let &t_SR = "\e[4 q"
  " Block in normal mode
  let &t_EI = "\e[2 q"
endif
" }}}

set backup
set completeopt^=menuone
set confirm
set cursorline
set define=
set expandtab
set hidden
set ignorecase
set include=
set lazyredraw
set linebreak
set matchpairs+=<:>
set matchtime=2
set pumheight=10
set scrolloff=2
set shell=/bin/sh
set shiftwidth=4
set showmatch
set sidescrolloff=5
set smartcase
set softtabstop=4
set splitbelow
set splitright
set nostartofline
set noswapfile
silent! set tagcase=match
set tags^=./.git/tags;
set virtualedit+=block
set wildcharm=<C-Z>
set wildignore+=*/.git/**/*,*/.hg/**/*,*/.svn/**/*
set wildignore+=tags
set wildignorecase
set wildmode=longest:full,full

if has('mouse')
  set mouse=a
endif

if has('folding')
  set foldlevelstart=99
endif

" Neovim specific settings
if has('nvim')
  set inccommand=nosplit
  set backupdir-=.
endif

" Enable undofile if possible
if has('persistent_undo')
  set undofile
endif

" Color the whole area outside of 'textwidth'
let &colorcolumn = '+' . join(range(1, 256), ',+')

" Indent wrapped lines
silent! set breakindent

" Specify whitespace chars
set listchars=tab:>\ ,trail:-,extends:>,precedes:<,nbsp:+

" Set default path to a simple value
set path=.,,

" Use a better grepprg if available
set grepformat^=%f:%l:%c:%m
if executable('rg')
  " Use rg over grep
  set grepprg=rg\ --vimgrep\ --smart-case
elseif executable('ag')
  " Use ag over grep
  set grepprg=ag\ --vimgrep\ --smart-case
elseif executable('grep')
  set grepprg=grep\ --line-number\ --recursive\ -I\ $*\ *
else
  set grepprg=internal
endif

" GUI settings
if has('gui_running')
  set lines=60
  set columns=180
  if has('win32')
    set guioptions-=t
  endif
endif

" Keep temporary files in a single location (nvim does this by default)
if !has('nvim')
  if exists('$XDG_DATA_HOME')
    let s:datadir = $XDG_DATA_HOME . '/vim'
  elseif has('unix')
    let s:datadir = $HOME . '/.local/share/vim'
  else
    let s:datadir = $HOME . '/vimfiles/cache'
  endif

  let &backupdir = s:datadir . '/backup//' . ',' . &backupdir
  let &undodir = s:datadir . '/undo//' . ',' . &undodir
  let &directory = s:datadir . '/swap//' . ',' . &directory

  " Create directories if they don't exist
  if exists('*mkdir')
    for opt in [&backupdir, &undodir, &directory]
      let dir = simplify(split(opt, ',')[0])
      if !isdirectory(dir)
        call mkdir(dir, 'p')
      endif
    endfor
  endif
endif

if filereadable($VIMHOME . '/tags')
  let &tags .= ',' . $VIMHOME . '/tags'
endif

" Set dictionary if it exists
if empty(&dictionary) && filereadable('/usr/share/dict/words')
  set dictionary+=/usr/share/dict/words
endif

" Enable histogram diff
if has('nvim-0.3.2') || has('patch-8.1.0360')
  set diffopt=filler,internal,algorithm:histogram,indent-heuristic
endif

" }}}

" Mappings {{{

" Insert {{{

" jk in Insert mode escapes to Normal mode
inoremap jk <Esc>l

" Set a new 'undo point' before undoing entire line
inoremap <C-U> <C-G>u<C-U>
inoremap <CR> <C-]><C-G>u<CR>

" Auto close braces in insert mode
inoremap {<CR> {<CR>}<Esc>ko

" Insert current date with <C-\>d
inoremap <silent> <C-\>d <C-R>=strftime('%Y-%m-%d')<CR>

" Insert current filename with <C-\>f
inoremap <silent> <C-\>f <C-R>=expand('%:t')<CR>

" Insert full path to file with <C-\>F
inoremap <silent> <C-\>F <C-R>=expand('%:p')<CR>

" Copy line above/below (similar to <C-Y>/<C-E> but works on the whole line)
inoremap <expr> <C-G><C-Y> repeat('<C-Y>', len(getline(line('.')-1))-col('.')+1)
inoremap <expr> <C-G><C-E> repeat('<C-E>', len(getline(line('.')-1))-col('.')+1)

" }}}

" Normal {{{

" Make Y work like D and C
nnoremap Y y$

" Switch to previous buffer
nnoremap <BS> <C-^>

" Navigate through wrapped lines individually
nnoremap <expr> j v:count ? 'j' : 'gj'
nnoremap <expr> k v:count ? 'k' : 'gk'

" Map Q to gq
noremap Q gqap

" Write file with <Space>w
nnoremap <Space>w :w<CR>

" Buffer shortcuts
for i in range(1, 10)
  exe 'nnoremap <Space>' . (i % 10) ':b' . i . '<CR>'
endfor

" List buffers and put :b on the command line
noremap <Space>b :buffers<CR>:b<Space>

" Find files in path
nnoremap <Space>f :find *

" Search for a tag by regex
nnoremap <Space>] :tjump /
nnoremap <Space>} :ptjump /

" Show tag stack
nnoremap <Space>t :tags<CR>

" Open quickfix list
nnoremap <silent> <Space>q :<C-U>call qf#toggle(0)<CR>
nnoremap <silent> <Space>l :<C-U>call qf#toggle(1)<CR>

" Find files in current buffer's directory
nnoremap <Space>e :e %:p:h/<C-Z>

" Budget fuzzy file finder
nnoremap <C-P> :e **/*

" <Bslash>ev opens .vimrc in new tab
nnoremap <Bslash>ev :<C-U>tabe $MYVIMRC<CR>:lcd %:h<CR>

" <Bslash>sv sources .vimrc
nnoremap <Bslash>sv :<C-U>source $MYVIMRC<CR>

" Clear search buffer with <C-L>
nnoremap <silent> <C-L> :<C-U>nohlsearch<Bar>diffupdate<Bar>syn sync fromstart<CR><C-L>
vnoremap <silent> <C-L> :<C-U>nohlsearch<Bar>diffupdate<Bar>syn sync fromstart<CR>gv<C-L>

" Format whole buffer with formatprg without changing cursor position
" See :h restore-position
nnoremap <Space>gq mzHmygggqG`yzt`z

" Modify contents of a register
nnoremap yr :<C-\>e'let @' . v:register . ' = ' . string(getreg(v:register))<CR><C-F>

" Show marks
nnoremap <Space>m :marks<CR>:norm! `

" Show only user placed marks (A-Z and a-z)
let s:alphabet = join(map(
      \ range(char2nr('A'), char2nr('Z')) + range(char2nr('a'), char2nr('z')),
      \ 'nr2char(v:val)'), '')
execute 'nnoremap <Space>M :marks ' . s:alphabet . '<CR>'
unlet s:alphabet

" Allow [[ and friends to work when opening brace is not in the first column
noremap <silent> [[ ?{<CR>w99[{:let @/ = ''<CR>
noremap <silent> ][ /}<CR>b99]}:let @/ = ''<CR>
noremap <silent> ]] j0?{<CR>w99[{%/{<CR>:let @/ = ''<CR>
noremap <silent> [] k$/}<CR>b99]}%?}<CR>:let @/ = ''<CR>

" }}}

" Visual {{{
xnoremap * y/\V<C-R>"<CR>
xnoremap # y?\V<C-R>"<CR>
" }}}

" Terminal {{{

" Make neovim terminal behave like the vim terminal
if has('nvim')
  tnoremap <C-W> <C-\><C-N><C-W>
  tnoremap <C-W>: <C-\><C-N>:
  tnoremap <C-W>. <C-W>
  tnoremap <C-W><C-\> <C-\>
  tnoremap <C-W>N <C-\><C-N>
  tnoremap <expr> <C-W>" '<C-\><C-N>"' . nr2char(getchar()) . 'pi'
  tnoremap <C-W><C-C> <C-D>
endif
" }}}

" Command {{{
cnoremap <C-P> <Up>
cnoremap <C-N> <Down>

cnoremap <C-X><C-E> <C-F>
" }}}

" }}}

" Abbreviations {{{

" Command {{{
" Sudo save
cnoreabbrev w!! w !sudo tee > /dev/null %
" }}}

" }}}

" Commands {{{

" This comes from vimrc_example.vim. See :h :DiffOrig
command! DiffOrig vert new | set bt=nofile | r ++edit # | 0d_ | diffthis
      \ | wincmd p | diffthis

" Edit ftplugin file for current filetype
command! EditFtplugin
      \ if empty(&ft) |
      \   echohl Error | echo 'Error: This buffer has no filetype' |
      \ else |
      \   execute 'tabe ' . $VIMHOME . '/after/ftplugin/' . &ft . '.vim' |
      \ endif

" }}}

" Autocommands {{{
augroup vimrc
" Have Vim jump to the last position when reopening a file
autocmd BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$")
      \ | exe "normal! g`\"" | endif

" Set marks by filetype for quick navigation
autocmd BufLeave *.h mark H
autocmd BufLeave *.{c,cc,cpp} mark C

" Start insert immediately when terminal opens
if exists('##TermOpen')
  autocmd TermOpen * setlocal nonumber | startinsert
endif

if has('nvim')
  autocmd BufEnter term://* startinsert
endif

" Create syntax group for trailing whitespace in each new buffer
autocmd Syntax * syn match TrailingWhitespace /\s\+\%#\@<!$/ containedin=ALL

" Enable omnicompletion from syntax if no other option available
" See :h ft-syntax-omni
if exists('+omnifunc')
  autocmd FileType *
        \ if &omnifunc == '' |
        \   setlocal omnifunc=syntaxcomplete#Complete |
        \ endif
endif

augroup END
" }}}

" vim: fdl=0
